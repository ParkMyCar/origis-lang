main = { SOI ~ (stmt*) ~ EOI }

stmt = { var_def | (expr ~ ";") }

expr_inner = _{ value | ident }

expr = { op_expr | expr_inner }

op_term = { "(" ~ op_expr ~ ")" | expr_inner }
op_expr = { (op_term ~ operator ~ op_term ) ~ (operator ~ op_term)* }

var_def = { ^"let" ~ ident ~ "=" ~ expr ~ ";" }

func_def = { ^"fn" ~ ident ~ "(" ~ func_def_params ~ ")" ~ _type ~ "{" ~ func_def_body ~ "}" }
func_def_param = _{ ident ~ ("," ~ ident)* ~ ":" ~ _type }
func_def_params = { (func_def_param ~ (func_def_param ~ ",")* ~ ","?)? }
func_def_body = { stmt* ~ expr? }

ident = ${ (!ASCII_DIGIT ~ ident_range) ~ ident_range* }
ident_range = _{ !WHITESPACE ~ (XID_CONTINUE | XID_START) }

_type = {
    int_type
    | float_type
    | char_type
}

int_type = { ^"int" }
float_type = { ^"float" }
char_type = { ^"char" }

value = {
    primitive_value
    | string
    | array
    | tuple
}

primitive_value = {
    char
    | float
    | integer
}

params = _{ (expr ~ ("," ~ expr)* ~ ","?)? }

array = { "[" ~ params ~ "]" }

tuple = { "(" ~ (expr ~ ("," ~ expr)+ | expr ~ ",")? ~ ")" }

string = ${ PUSH("\"") ~ (escape | (!("\\" | "\"") ~ ANY)+)* ~ POP}

char = ${
    PUSH("'") ~ (escape | (!("\\" | "\"") ~ ANY)) ~ POP
}

escape = _{
    "\\\\"
    | "\\\""
    | "\\'"
    | "\\n"
    | "\\r"
    | "\\t"
}

// ops

operator = {
    op_add
    | op_sub
    | op_mul
    | op_div
    | op_mod
    | op_pow
    | op_xor
}

op_add = { "+" }
op_sub = { "-" }
op_mul = { "*" }
op_div = { "/" }
op_mod = { "%" }
op_pow = { "**" }
op_xor = { "^" }

float = @{ (integer_dec ~ "." ~ ASCII_DIGIT+) }

integer = ${
    integer_hex
    | integer_oct
    | integer_bin
    | integer_dec
}

integer_dec = { ("+" | "-")? ~ ASCII_DIGIT+ }

integer_oct = { ("+" | "-")? ~ ^"0o" ~ ASCII_OCT_DIGIT+ }

integer_hex = { ("+" | "-")? ~ ^"0x" ~ ASCII_HEX_DIGIT+ }

integer_bin = { ("+" | "-")? ~ ^"0b" ~ ASCII_BIN_DIGIT+ }

WHITESPACE = _{
    " "
    | "\t"
    | WHITE_SPACE
    | NEWLINE
}

COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE? | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
